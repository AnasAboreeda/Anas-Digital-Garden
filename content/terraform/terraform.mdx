---
id: 2023-01-18-terraform
title: Terraform
metaTitle: Terraform | Anas's Digital Garden
metaDescription: terraform | Anas's Digital Garden
createdDate: 2023-01-18
updatedDate: 2023-01-26
weekNumber: 202303
category: terraform
tags: ["terraform", "terraform"]
---

# Terraform

## Introduction

**What is Terraform?**

Terraform is a tool for building, changing, and versioning infrastructure safely and efficiently.

## Two Parts of Terraform

Terraform is actually split into two parts:

- One part is the Terraform engine that knows how to get from the state your infrastructure is currently into the state you want your infrastructure to be in.
- The other part is the provider, which talks to the infrastructure to find out the current state and make changes using the infrastructure's API.

Due to the clever way Terraform is split, there are providers available for practically anything you can think of.

## Terraform Language

Terraform uses a language called Hashicorp Markup Language, or HCL as it is known. HCL is a very simple, easy-to-read syntax that is understandable even to people looking at it for the first time.

## Terraform Security

Terraform allows you to see a preview of what it is going to do (plan) and awaits your confirmation before it actually makes any changes. This is a great safety net in case you made an accidental change.

## Add Terraform to an Existing Infrastructure

- You can do this simply by importing your infrastructure into Terraform.
- This is great as it allows you to move your infrastructure from being manually set up to be defined in code.

## Terraform vs. Cloudformation

There are several reasons why Terraform is a better choice than CloudFormation for your project.

**Faster Terraform

- Terraform is open source and generally moves faster than CloudFormation.

**CloudFormation readability

- CloudFormation uses JSON or YAML for configuration. Both of these formats are flawed, in my view, for different reasons.

**Terraform readability

- Terraform uses HCL, which has a clean, concise syntax. It is very easy to read, allows comments (both inline and block), and is not fussy about spacing, newlines, or indentation.
- Using HCL, you can easily split your project up into multiple files as you see fit to make the code easier to read and understand when coming to the project.

**Terraform's pinnacle killer feature

- You can use Terraform to configure all of your infrastructures, whereas CloudFormation only works for AWS.

## Chef and Puppet vs. Terraform

- Chef and Puppet are configuration management tools.
- They are designed to configure and manage the software that is running on a machine (infrastructure) that already exists.
- Terraform, on the other hand, sits above that at the abstraction layer and is designed to set up all of the infrastructures that make up your system, such as load balancers, servers, DNS records, etc.
- In other words, Puppet and Chef are used to configure servers, while Terraform is used to create the server itself.

## First Project

main.tf

```bash
provider "aws" {
    region = "eu-west-1"
}

resource "aws_s3_bucket" "first_bucket" {
    bucket = "adam-first-bucket"
}
```

```bash
# initialize the project
$ terraform init

# Create the infra
$ terraform apply

# Destroy the infra
$ terraform destroy
```

## Resources in Detail

### Terraform Resources

- Resources in Terraform represent things in the real world. For example, a resource could be an AWS Load Balancer, an alarm in PagerDuty, a policy in Vault, etc.
- Remember, you can create resources representing things from multiple vendors (for example, multiple clouds) in a single project.
- The resource type `aws_s3_bucket` starts with the name of the provider followed by an underscore (`aws_`). This allows you to tell just from the first word of the resource which vendor or component this resource will be created in.

### Examples

```bash
resource "google_folder" "department" {
    display_name = "Department"
    parent = "organisation/1234567"
}
```

The department resource above will create a folder in Google Cloud (GCP). You can see that it starts with `google_` which is the name of the Google Cloud (GCP) provider. Every resource for this provider will start with `google_`.

```bash
resource "postgresql_role" "my_role" {
    name = "my_role"
    login = true
    password = "password123"
}
```

The `my_role` resource above will create a login on a Postgres database, with login name `my_role` and password `password123` . The resource name starts with `postgresql_` as every resource for the Postgres provider will.

- Check the documentation to get all the resource properties e.g.

[Terraform Registry](https://registry.terraform.io/providers/hashicorp/aws/latest/docs/resources/s3_bucket)

### Interpolation Syntax

It's important to use the resource output to help terraform define the dependencies.

### Useful Resource Output

- Once a resource is created, it returns a number of attributes.
- The attributes a resource returns can be found in the ‚Äú*Attributes Reference*‚Äù section on the documentation page for any resource.
- This is amazingly useful as it allows you to use the output from one resource as the argument for another resource.

```bash
provider "aws" {
    region = "eu-west-1"
}

resource "aws_vpc" "my_vpc" {
    cidr_block = "10.0.0.0/16"
}

resource "aws_security_group" "my_security_group" {
    vpc_id = aws_vpc.my_vpc.id
    name = "Example security group"
}

resource "aws_security_group_rule" "tls_in" {
    protocol = "tcp"
    security_group_id = aws_security_group.my_security_group.id
    from_port = 443
    to_port = 443
    type = "ingress"
    cidr_blocks = ["0.0.0.0/0"]
}

resource "aws_security_group_rule" "http_in" {
    protocol = "tcp"
    security_group_id = aws_security_group.my_security_group.id
    from_port = 80
    to_port = 80
    type = "ingress"
    cidr_blocks = ["0.0.0.0/0"]
}
```

- This project creates an AWS VPC with CIDR block `10.0.0.0/16`.
- Then it defines a security group (`aws_security_group`).
- Notice that the value of `vpc_id` is set to `aws_vpc.my_vpc.id` in the definition of the security group.
- The value of `aws_vpc.my_vpc.id` is not known before we run the project, since AWS will randomly assign it when we create the VPC.
- By referencing the VPC we created, we can use this value even though we do not know what it will be until we run the project.

### Output Attribute

- The format for using an output attribute from a resource is

 `<resource_type>.<resource_identifier>.<attribute_name>`.

- In the VPC id example, we are getting the output from an `aws_vpc` resource type, with the identifier name `my_vpc`, and we want to get the id attribute value.
- Thus, we end up with `aws_vpc.my_vpc.id`.

### Parallelism in Terraform

- When you run the project, Terraform will realize that it can create both security group rules in parallel.
- Once the security group they both depend on is created, it will create both of the rules together.
- This feature of Terraform makes for excellent performance. It may seem obvious in this example, but as a project grows, the amount Terraform can run in parallel can be quite impressive.

## Providers in Detail

### Terraform Providers

- A provider in Terraform is a connection that allows Terraform to manage infrastructure using a pre-defined interface.
- This abstraction means the provider is completely separate from the Terraform engine - which understands how to read state from a provider, read HCL code, and work out how to get to the desired state.
- This allows you to write a provider to connect to anything that has a programmable way to talk to it.
- All the writer of a provider has to do for each resource they want Terraform to control is provide Terraform with a way to create it, read it, and delete it.

### Provider Model

- Due to the provider model that Terraform employs, providers are not part of the main Terraform source code.
- They are separate binaries that live in their own repositories and can move at their own speed.
- This means that if a provider needs to release a bug fix or new feature, they can release it. They do not need to coordinate a release of the main Terraform code base.

### Where Terraform Puts the Provider

What happens to the provider then? Terraform simply puts it inside the project where you are currently working in a special folder called `.terraform`.

The provider is actually a separate binary that Terraform calls out at a run time to do its work.

## Provider Best Practices

- Commit `.terraform.lock.hcl` file to the git to lock the versions.

> Terraform has created a lock file .terraform.lock.hcl to record the provider
selections it made above. Include this file in your version control repository
so that Terraform can guarantee to make the same selections by default when
you run "terraform init" in the future.
>

- You can control the version of the providers like this

```bash
provider "aws" {
  region  = "eu-west-1"
}

terraform {
  required_providers {
    aws = {
      version = "~> 3.46"
    }
  }
}
```

### Creating Multiple Instances of the Same Provider

```bash
provider "aws" {
    region = "eu-west-1"
}

provider "aws" {
    region = "eu-west-2"
    alias = "london"
}

resource "aws_vpc" "ireland_vpc" {
    cidr_block = "10.0.0.0/16"
}

resource "aws_vpc" "london_vpc" {
    cidr_block = "10.1.0.0/16"
    provider = "aws.london"
}

```

- Alias is a property you can set on any provider block. What this gives us is a way to distinguish between the two providers.
- Once you define two or more instances of the same provider, every definition must have an alias set after the first.

## Data Sources in Detail

### Terraform Data Source

- A data source in Terraform is used to fetch data from a resource that is not managed by the current Terraform project.
- This allows it to be used in the current project.
- You can think of it as a read-only resource that already exists; the object exists, but you want to read specific properties of that object for use in your project.

```bash
provider "aws" {
    region = "eu-west-1"
}

data "aws_s3_bucket" "bucket" {
    bucket = "kevholditch-already-exists"
}

resource "aws_iam_policy" "my_bucket_policy" {
    name = "my-bucket-policy"

    policy = <<POLICY
    {
        "Version": "2012-10-17",
        "Statement": [
            {
                "Action": [
                    "s3:ListBucket"
                ],
                "Effect": "Allow",
                "Resource": [
                    "${data.aws_s3_bucket.bucket.arn}"
                ]
            }
        ]
    }
    POLICY
}
```

- As you can see from the above project, a data source block starts with the word ‚Äúdata‚Äù.
- The next word is the type of data source. We are using an `aws_s3_bucket` data source, which is used to lookup an S3 bucket.
- After the data source type, we give the data source an identifier, in this case `"bucket"`.
- The identifier is used to reference the data source inside the Terraform project.
- The data source block is opened with a `{`. You then specify any properties you want Terraform to use to search for the resource. We are using the complete name of the S3 bucket we are looking for. You then close the data source block with `}`.

### Referencing a Bucket

- Rather than creating the bucket as we did before, here we are referencing a bucket that already exists.
- So, before you run the above project, you will need to create an S3 bucket with the name that you specify inside the data block.
- In the example above, the bucket would be called `kevholditch-already-exists`.
- Name the bucket anything you want, but make sure to paste the name into the bucket property in the data source.

### Aws Iam Policy

- At the bottom of this project, we create an AWS IAM policy that gives permissions to list the bucket that we looked up in the data source.
- There are a couple of new concepts in the `aws_iam_policy` resource that we first need to introduce.
1. The IAM policy itself is a multi-line string enclosed in between `<<POLICY` and `POLICY`.
- This is how you define a multi-line string in Terraform. You open the multi-line string with `<<` then place any identifier you wish as a single word.
- I have used `POLICY` in the example above since we are defining an IAM policy, but you can use any label like `<<STATEMENT` or `<<IAM`. You then start your multi-line string on the next line and finish it with the opening identifier without the `<<`.

> üìù**Note**: The closing marker must be at the start of a new line. Otherwise, it is a syntax error.
>

### Terraform Data Source Advantages

- As your Terraform project grows it can be sensible to break it up into smaller projects to make for easier maintenance
    - Imagine you want to create a new AWS EC2 instance using an AMI image from a private repository.
    - You could hard code the name of the AMI image when creating the instance and manually update it when a new AMI image is released.
    - This would technically work, but is quite cumbersome and would require a code change every time you wanted to use the latest version of the AMI image.
    - By using a data source instead, you can set things up to always read the repository and retrieve the latest version of the AMI image when you run Terraform.
    - You can then reference the data source when creating the EC2 instance to ensure that you always have the latest version of the image.
- Another use for data sources is when you are migrating existing infrastructure to Terraform and want to reference a resource that is not part of your Terraform project yet.

### Q/A

- What is the syntax that is used to refer to an output from a data block?

`data.<data_type>.<data_identifier>.<attribute_name>`

## Outputs Explained

### Terraform Project Output

- Output in your Terraform project shows a piece of data after Terraform successfully completed.
- Outputs are useful as they allow you to echo values from the Terraform run to the command line.
- For example, if you are creating an environment and setting up a bastion jump box as part of that environment, it's handy to be able to echo the public IP address of the newly created bastion to the command line.
- Then, after the `terraform apply` finishes, you are given the IP of the newly created bastion which is ready for you to ssh straight onto it.

```bash
provider "aws" {
  region = "eu-west-1"
}

resource "aws_s3_bucket" "first_bucket" {
  bucket = "adamtest23444-bucket-outputs"
}

output "bucket_name" {
  value = aws_s3_bucket.first_bucket.id
}

output "bucket_arn" {
  value = aws_s3_bucket.first_bucket.arn
}

output "bucket_information" {
  value = "bucket name: ${aws_s3_bucket.first_bucket.id}, bucket arn: ${aws_s3_bucket.first_bucket.arn}"
}

output "all" {
  value = aws_s3_bucket.first_bucket
}
```

### Exporting All Attributes

As of Terraform version `1.0.0`, Terraform allows you to output an entire resource or data block. To do this, we'll take the example that we just had in the last lesson and added the `output "all"` block

## Locals in Detail

### Terraform Local

- A local is Terraform's representation of a normal programming language variable.
- Confusingly, Terraform also has a concept called a variable which is really an input.
- A local can refer to a fixed value such as a string, or it can be used to refer to an expression such as two other locals concatenated together.

```bash
provider "aws" {
    region = "eu-west-1"
}

locals {
    first_part = "hello"
    second_part = "${local.first_part}-there"
    bucket_name = "${local.second_part}-how-are-you-today"
}

resource "aws_s3_bucket" "bucket" {
    bucket = local.bucket_name
}
```

## Files

## Terraform Files

- We have seen in the **[Data Sources in Detail](terraform.md)** how you can place a multi-line string as a value for a property. This is useful for something like an IAM policy.
- It can be even cleaner to move the value out into a file and then reference that file from your project. Doing this may remove the clutter from your project and make it much more readable.

```bash
provider "aws" {
    region = "eu-west-1"
}

resource "aws_iam_policy" "my_bucket_policy" {
    name = "list-buckets-policy"
    policy = file("./policy.iam")
}
```

### Templatefile Function

```bash
# main.tf

locals {
    rendered = templatefile("./example.tpl", { name = "kevin", number = 7})
}

output "rendered_template" {
    value = local.rendered
}
```

```bash

# example.tpl

hello there ${name}
there are ${number} things to say
```

**Output**

```bash
Apply complete! Resources: 0 added, 0 changed, 0 destroyed.

Outputs:

rendered_template =
hello there kevin
there are 7 things to say
```

You can see that Terraform replaced the placeholders in our template with the values that we provided to the `templatefile` function. Thus, we see the message with `Kevin` and `7` in it rather than the placeholders.

### Loops in a Template

You can pass in an array of values into a template and loop through them.

```bash
# main.tf

output "rendered_template" {
    value = templatefile("./backends.tpl", { port = 8080, ip_addrs = ["10.0.0.1", "10.0.0.2"] })
}
```

```bash
# backends.tpl

%{ for addr in ip_addrs ~}
backend ${addr}:${port}
%{ endfor ~}
```

**Output**

```bash
backend 10.0.0.1:8080
backend 10.0.0.2:8080
```

### Combine Loops

The fact that we can combine loops with interpolated values means that you can write some quite elaborate templates. These can be useful for programmatically generating things like IAM policies, where you can pass in the ARN of resources that Terraform creates and use as part of the template

## Variables

A variable in Terraform is something that can be set at runtime. It allows you to vary what Terraform will do by passing in or using a dynamic value.

```bash
provider "aws" {
    region = "eu-west-1"
}

variable "bucket_name" {
    description = "the name of the bucket you wish to create"
}

resource "aws_s3_bucket" "bucket" {
    bucket = var.bucket_name
}
```

> üìù**Note**: It is often a good idea to provide a `description` as it gives the user some indication as to what values to use and what the `variable` is used for.
>

### Variable Defaults

```bash
provider "aws" {
    region = "eu-west-1"
}

variable "bucket_name" {
    description = "the name of the bucket you wish to create"
}

variable "bucket_suffix" {
    default = "-abcd"
}

resource "aws_s3_bucket" "bucket" {
    bucket = "${var.bucket_name}${var.bucket_suffix}"
}
```

### Setting a Variable Using Command line

```bash
$ terraform apply -var bucket_name=kevholditch -var bucket_suffix=foo
```

In this command, we are giving a value to both the `bucket_name` and `bucket_suffix`. If you run the project with the above command to set both variables, then Terraform will not stop to request a value of the `bucket_name` . This is because you have now provided one. Terraform will stop and ask for a value of any defined variable that does not have a default value or other set value

### Setting a Variable Using an Environment Variable

Another way you can set the value of a variable is by using an environment variable. To do this, set an environment variable in your terminal using the convention `TF_VAR_<variable_identifier>` .

```bash
export TF_VAR_bucket_name=<your-bucketname>
export TF_VAR_bucket_suffix=via-env
```

Once you have set the environment variables, run `terraform apply` . You will see that now Terraform will not ask you for a value for either variable and will use the values from your environment variables.

### Setting a Variable Using a File

Another way to set the value of variables is by using a file. We'll create a new file in the project called `terraform.tfvars` .

```bash
# terraform.tfvars

bucket_name = "<your-bucketname>"
bucket_suffix = "from-file"
```

- `terraform.tfvars` is a special filename that Terraform looks at to discover values for variables.
- Terraform will look in this file and use any values given for a variable.
- To set the value of the variable you simply put the variable identifier, an equals sign, and the value you want to give it.
- We are setting the value of both `bucket_name` and `bucket_suffix` in our file. Now when we run the project, Terraform will use those values for the variables and not ask us for them.

### Another File name

- The other way we could have named our file is by using anything ending in `.auto.tfvars`.
- Terraform examines files with that ending for set variables. It is also possible to define multiple files and put the value for different variables in each of them.

### More Complex Variables

```bash
# terraform.tfvars

instance_map = {
    dev = "t3.small"
    test = "t3.medium"
    prod = "t3.large"
}

environment_type = "dev"
```

```bash
# main.tf

variable "instance_map" {}
variable "environment_type" {}

output "selected_instance" {
    value = var.instance_map[var.environment_type]
}
```

**Running the project

- If you run this project as is, it will output `selected_instance = t3.small`. This is because `t3.small` is the value in the map for the key dev and we have set the `environment_type` to dev. Change the environment type to one of the other values in the map, run the project again, and you will see the output change
- Since the map of instances is a variable, we could dynamically change this too. For instance, we could have a different `terraform.tfvars` file per department for example. This allows us to vary the instance sizes used for different environment types by the department.

### Type Constraints - Simple Types

A type constraint allows you to be specific about the type of a variable. There are three simple types:

- string
- number
- bool

```bash
variable "a" {
    type = string
    default = "foo"
}

variable "b" {
    type = bool
    default = true
}

variable "c" {
    type = number
    default = 123
}

output "a" {
    value = var.a
}

output "b" {
    value = var.b
}

output "c" {
    value = var.c
}
```

### Using Type Constraint

- By using a type constraint, we make it illegal to set the variable to anything other than the type defined.
- So, for example, if we try to set `b` to `"hello"` or `123` and run Terraform, it will print an error saying that the value we have provided is not compatible with the type constraint.

There are a few interesting quirks with how the value you give is interpreted that are worth noting.

- When we define the type to be bool, the following values will be valid:
    - `true`
    - `false`
    - `‚Äútrue‚Äù`
    - `"false"`
    - `"1"` (evaluated to `true`)
    - `"0"` (evaluated to `false`)
- `"1"` is valid but `1` (without the quotes) is not valid, which may be puzzling at first.
- With a `number`, any valid number will be allowed with or without quotes.
- If you specify quotes then you are essentially defining a string. As long as the string only contains digits, Terraform will realise this and implicitly convert the value to a number.
- With a string, any value in quotes will be allowed.

### Why Use Type Constraint

The fact that Terraform coalesces variables (attempts to see what a variable is) is one of the reasons that it's a good idea to use a type constraint. Another reason is that it guides the person using the Terraform code as to what value to use for the variable and improves code readability.

## Complex Types

In addition to the three simple types above, these types can be combined into the following more complex types:

- `list(<TYPE>)`
- `set(<TYPE>)`
- `map(<TYPE>)`
- `object()`
- `tuple([<TYPE>, ‚Ä¶])`

For each of the complex types, you can use another complex type or simple type where you see the word `<TYPE>`. So you can have a list of numbers, a list of strings, a list of maps of strings, etc.
